!define COPY_STR "© 2008-2010"
!define DEV_STR "Alexey Tatuyko"
!define PROG_NAME "BirEdit"
!define PRODUCT_MAJOR "2"
!define PRODUCT_MINOR "0"
!define PRODUCT_RELEASE "2"
!define PRODUCT_REVISION "68"
!define PRODUCT_NAME "${PROG_NAME} ${PRODUCT_MAJOR}.${PRODUCT_MINOR}.${PRODUCT_RELEASE}.${PRODUCT_REVISION}-ansi"
!define PRODUCT_OUTFILE "biredit-${PRODUCT_MAJOR}.${PRODUCT_MINOR}.${PRODUCT_RELEASE}.${PRODUCT_REVISION}-win9x-setup.exe"

Name "${PRODUCT_NAME}"
OutFile "${PRODUCT_OUTFILE}"
InstallDir "$PROGRAMFILES\${PROG_NAME}\"

VIAddVersionKey /LANG=1033 "ProductName" "${PROG_NAME} text editor"
VIAddVersionKey /LANG=1033 "Comments" "${PROG_NAME} text editor setup file"
VIAddVersionKey /LANG=1033 "CompanyName" "${DEV_STR}"
VIAddVersionKey /LANG=1033 "LegalCopyright" "${COPY_STR} ${DEV_STR}"
VIAddVersionKey /LANG=1033 "FileDescription" "${PROG_NAME} text editor setup file"
VIAddVersionKey /LANG=1033 "FileVersion" "${PRODUCT_MAJOR}.${PRODUCT_MINOR}.${PRODUCT_RELEASE}.${PRODUCT_REVISION}-ansi"
VIAddVersionKey /LANG=1033 "ProductVersion" "${PRODUCT_MAJOR}.${PRODUCT_MINOR}.${PRODUCT_RELEASE}.${PRODUCT_REVISION}-ansi"
VIProductVersion "${PRODUCT_MAJOR}.${PRODUCT_MINOR}.${PRODUCT_RELEASE}.${PRODUCT_REVISION}"

Page license
Page components
Page directory
Page instfiles

UninstPage uninstConfirm
UninstPage instfiles

LicenseData "..\docs\license.txt"

LangString DevStr ${LANG_ENGLISH} "${DEV_STR}"
LangString DevStr ${LANG_RUSSIAN} "Алексей Татуйко"
LangString Str1 ${LANG_ENGLISH} "Program"
LangString Str1 ${LANG_RUSSIAN} "Программа"
LangString Str2 ${LANG_ENGLISH} "Languages"
LangString Str2 ${LANG_RUSSIAN} "Языки интерфейса"
LangString Str3 ${LANG_ENGLISH} "English"
LangString Str3 ${LANG_RUSSIAN} "Английский"
LangString Str4 ${LANG_ENGLISH} "Russian"
LangString Str4 ${LANG_RUSSIAN} "Русский"
LangString Str5 ${LANG_ENGLISH} "Shortcuts"
LangString Str5 ${LANG_RUSSIAN} "Ярлыки"
LangString Str6 ${LANG_ENGLISH} "Quick launch"
LangString Str6 ${LANG_RUSSIAN} "Панель быстрого запуска"
LangString Str7 ${LANG_ENGLISH} "Desktop"
LangString Str7 ${LANG_RUSSIAN} "Рабочий стол"
LangString Str8 ${LANG_ENGLISH} "Start menu"
LangString Str8 ${LANG_RUSSIAN} "Пуск"
LangString Str_Un ${LANG_ENGLISH} "Uninstall"
LangString Str_Un ${LANG_RUSSIAN} "Удалить"

BrandingText "${COPY_STR} $(DevStr)"

Section $(Str1)
SectionIn Ro
  SetOutPath "$INSTDIR"
  File /oname=biredit.exe biredit.exe
  File /oname=changelog.txt changelog.txt
  File /oname=help.en.htm help.en.htm
  File /oname=help.ru.htm help.ru.htm
  File /oname=license.txt license.txt
  File /oname=profile.ini profile.ini.dist
  SetOutPath "$INSTDIR\plugins"
  File /oname=listfolderplugin.dll "plugins\listfolderplugin.dll"
  File /oname=listfolderplugin.ini "plugins\listfolderplugin.ini"
  SetOutPath "$INSTDIR\syn"
  File /r "syn\*.*"
  SetOutPath "$INSTDIR"
  WriteUninstaller "$INSTDIR\uninstall.exe"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "DisplayName" "${PROG_NAME}"
  WriteRegExpandStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "UninstallString" "$INSTDIR\uninstall.exe"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "InstallLocation" "$INSTDIR"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "DisplayIcon" "$INSTDIR\biredit.exe,0"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "Publisher" "$(DevStr)"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "HelpLink" "http://biredit.googlecode.com/"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "URLUpdateInfo" "http://biredit.googlecode.com/"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "URLInfoAbout" "http://biredit.googlecode.com/"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "DisplayVersion" "${PRODUCT_MAJOR}.${PRODUCT_MINOR}.${PRODUCT_RELEASE}.${PRODUCT_REVISION}-ansi"
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "VersionMajor" 0x00000002
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "VersionMinor" 0x00000000
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "NoModify" 0x00000001
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}" "NoRepair" 0x00000001
SectionEnd

SectionGroup /e $(Str2)
Section $(Str3)
  SetOutPath "$INSTDIR\lang"
  File /oname=english.lng lang\english.lng
SectionEnd
Section $(Str4)
  SetOutPath "$INSTDIR\lang"
  File /oname=russian.lng lang\russian.lng
SectionEnd
SectionGroupEnd

SectionGroup $(Str5)
Section $(Str6)
  SetOutPath "$QUICKLAUNCH"
  CreateShortCut "$QUICKLAUNCH\${PROG_NAME}.lnk" "$INSTDIR\biredit.exe" \
  "" "$INSTDIR\biredit.exe" 0 SW_SHOWNORMAL
SectionEnd
Section $(Str7)
  SetOutPath "$DESKTOP"
  CreateShortCut "$DESKTOP\${PROG_NAME}.lnk" "$INSTDIR\biredit.exe" \
  "" "$INSTDIR\biredit.exe" 0 SW_SHOWNORMAL
SectionEnd
Section $(Str8)
  SetOutPath "$SMPROGRAMS"
  CreateDirectory "$SMPROGRAMS\${PROG_NAME}"
  CreateShortCut "$SMPROGRAMS\${PROG_NAME}\${PROG_NAME}.lnk" "$INSTDIR\biredit.exe" \
  "" "$INSTDIR\biredit.exe" 0 SW_SHOWNORMAL
  CreateShortCut "$SMPROGRAMS\${PROG_NAME}\$(Str_Un).lnk" "$INSTDIR\uninstall.exe" \
  "" "$INSTDIR\uninstall.exe" 0 SW_SHOWNORMAL
SectionEnd
SectionGroupEnd

Section "Uninstall"
  Delete /REBOOTOK "$DESKTOP\${PROG_NAME}.lnk"
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PROG_NAME}"
  Exec '"$INSTDIR\biredit.exe" /u'
  RMDir /r /REBOOTOK "$INSTDIR"
SectionEnd

Function .onInit
	Push ""
	Push ${LANG_ENGLISH}
	Push "English"
	Push ${LANG_RUSSIAN}
	Push "Русский"
	Push A
	LangDLL::LangDialog "Installer Language" "Please select the language of the installer"
	Pop $LANGUAGE
	StrCmp $LANGUAGE "cancel" 0 +2
		Abort
FunctionEnd

Function un.onInit
	Push ""
	Push ${LANG_ENGLISH}
	Push "English"
	Push ${LANG_RUSSIAN}
	Push "Русский"
	Push A
	LangDLL::LangDialog "Uninstaller Language" "Please select the language of the uninstaller"
	Pop $LANGUAGE
	StrCmp $LANGUAGE "cancel" 0 +2
		Abort
FunctionEnd